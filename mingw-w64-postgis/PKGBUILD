# Maintainer: LI Xuzhang <lixuzhang@gmail.com>

_realname=postgis
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.5.0
pkgrel=1
pkgdesc="Spatial and Geographic objects for PostgreSQL (mingw-w64)"
arch=('any')
url="http://postgis.net/"
license=('GPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=( "${MINGW_PACKAGE_PREFIX}-gcc-libs"
          "${MINGW_PACKAGE_PREFIX}-gdal"
          "${MINGW_PACKAGE_PREFIX}-geos"
          "${MINGW_PACKAGE_PREFIX}-gettext"
          "${MINGW_PACKAGE_PREFIX}-json-c"
          "${MINGW_PACKAGE_PREFIX}-libxml2"
          "${MINGW_PACKAGE_PREFIX}-postgresql"
          #"${MINGW_PACKAGE_PREFIX}-protobuf-c"
          "${MINGW_PACKAGE_PREFIX}-proj")
options=('staticlibs' 'strip')
source=("http://download.osgeo.org/postgis/source/${_realname}-${pkgver}.tar.gz"
        pg_config)
sha256sums=('be73abd747e9665f29748e0e7dc6b3f7b1ce98f32e3567f08259420ca10e36d5'
            'cb6a909b2bd823d6091e75a5075f7fab3731b08abab7c734ea720ea944973e60')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-protobuf \
    --with-pgconfig=${srcdir}/pg_config

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
