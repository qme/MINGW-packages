# Maintainer: Michael Hansen <zrax0111 gmail com>

_realname=capstone
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-capstone"
         "${MINGW_PACKAGE_PREFIX}-python2-capstone"
         "${MINGW_PACKAGE_PREFIX}-python3-capstone")
_pkgver=3.0.5
pkgver=${_pkgver//-/}
pkgrel=2
pkgdesc='Lightweight multi-platform, multi-architecture disassembly framework'
url='https://www.capstone-engine.org/index.html'
arch=('any')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("${_realname}-${_pkgver}.tar.gz::https://github.com/aquynh/capstone/archive/${_pkgver}.tar.gz"
        "mingw_python.patch"
        "pkgconfig.patch")
sha512sums=('4d953dd34866046afd4c8896c307f8fdd514ea1331720cb643a6b7095666dbecf6a16264c15c9883a6ed5e06d1332c1e52c5a23bd9dc3cc6493592bc9636bd33'
            '0beb821db5b2e79aca6202042a1204e8ec42f1d980be588fb2c243f96813c005901f6a47d6de0965e26b28d52e8da02da60c459f611f4a6a1f4aed6ea629f24c'
            '47e6f281bafb87ffedf86dfdba025a61faaf70c0b90d62941d2e4ac5ec7d72daa53538a753e96eb26b1f260c8364ac6c3450b7b2ccffa834651f3accc635f7cf')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}/mingw_python.patch"
  patch -p1 -i "${srcdir}/pkgconfig.patch"
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-${_pkgver}

  make
}

package_capstone() {
  cd build-${MINGW_CHOST}

  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${_pkgver}/docs/README" -t "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}"
  install -Dm644 "${srcdir}/${_realname}-${_pkgver}/LICENSE.TXT" -t "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}"
}

package_mingw-w64-i686-capstone()   { package_capstone; }
package_mingw-w64-x86_64-capstone() { package_capstone; }

package_python2-capstone() {
  depends=("${MINGW_PACKAGE_PREFIX}-capstone" "${MINGW_PACKAGE_PREFIX}-python2")
  cd ${_realname}-${_pkgver}/bindings/python

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  LIBCAPSTONE_PATH="${srcdir}/build-${MINGW_CHOST}/libcapstone.dll" \
  python2 setup.py install -O1 --root="${pkgdir}" --prefix="${MINGW_PREFIX}"

  install -Dm644 "${srcdir}/${_realname}-${_pkgver}/LICENSE.TXT" -t "${pkgdir}/${MINGW_PREFIX}/share/licenses/python2-${_realname}"
}

package_mingw-w64-i686-python2-capstone()   { package_python2-capstone; }
package_mingw-w64-x86_64-python2-capstone() { package_python2-capstone; }

package_python3-capstone() {
  depends=("${MINGW_PACKAGE_PREFIX}-capstone" "${MINGW_PACKAGE_PREFIX}-python3")
  cd ${_realname}-${_pkgver}/bindings/python

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  LIBCAPSTONE_PATH="${srcdir}/build-${MINGW_CHOST}/libcapstone.dll" \
  python3 setup.py install -O1 --root="${pkgdir}" --prefix="${MINGW_PREFIX}"

  install -Dm644 "${srcdir}/${_realname}-${_pkgver}/LICENSE.TXT" -t "${pkgdir}/${MINGW_PREFIX}/share/licenses/python3-${_realname}"
}

package_mingw-w64-i686-python3-capstone()   { package_python3-capstone; }
package_mingw-w64-x86_64-python3-capstone() { package_python3-capstone; }
