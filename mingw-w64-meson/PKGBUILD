# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=meson
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.47.1
pkgrel=3
pkgdesc="High-productivity build system (mingw-w64)"
arch=('any')
url="http://mesonbuild.com/"
license=("Apache 2")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/mesonbuild/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz
        'color-term.patch'
        '0001-Install-scripts-without-.py-extension.patch'
        '0002-Default-to-sys.prefix-as-the-default-prefix.patch'
        '0003-Strip-the-prefix-from-all-paths-when-installing-with.patch'
        '0001-gtkdoc-set-PATH-on-Windows-when-executing-gtkdoc-sca.patch'
        '0001-python.find_installation-only-try-to-find-python-wit.patch'
        )
sha256sums=('d673de79f7bab064190a5ea06140eaa8415efb386d0121ba549f6d66c555ada6'
            'edec6de6f5fa60f0737fdb25a3ac276a0187a6f961544289e5472bcb4194e954'
            '2c3fbb9d3f367d97e7262dc2991a2ef36eb06a7af08227b9e525049471d968c5'
            '4ef1e6fb0e9bd927b4646e416631b8a0a9aece763d259dbba58168bcba93f673'
            '2093c617cf3146a4cea601e7c73400d8ec5fd52ac5cf642c4f5af2d6493b1cb1'
            '3ef4159951e0b4e6fa0434b7203437c3c535893af43e3c45838524ba5d953b6b'
            '37aeb73747343d0a4c9ca872ae510e1fac653bf63ba9668229c5dfe0525d9b10'
           )

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/color-term.patch

  patch -Np1 -i "${srcdir}"/0001-Install-scripts-without-.py-extension.patch
  patch -Np1 -i "${srcdir}"/0002-Default-to-sys.prefix-as-the-default-prefix.patch
  patch -Np1 -i "${srcdir}"/0003-Strip-the-prefix-from-all-paths-when-installing-with.patch

  # https://github.com/mesonbuild/meson/issues/3307
  patch -Np1 -i "${srcdir}"/0001-gtkdoc-set-PATH-on-Windows-when-executing-gtkdoc-sca.patch

  # https://github.com/mesonbuild/meson/pull/3910
  patch -Np1 -i "${srcdir}"/0001-python.find_installation-only-try-to-find-python-wit.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/meson"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonconf"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonintrospect"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesontest"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/wraptool"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
