# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=discount
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.3a
pkgrel=1
pkgdesc="A Markdown implementation written in C (mingw-w64)"
arch=('any')
url="http://www.pell.portland.or.us/~orc/Code/discount/"
license=('custom:BSD')
provides=("${MINGW_PACKAGE_PREFIX}-markdown")
conflicts=("${MINGW_PACKAGE_PREFIX}-markdown")
options=('strip' 'staticlibs')
source=(http://www.pell.portland.or.us/~orc/Code/discount/${_realname}-${pkgver}.tar.bz2
        discount-mingw-building.patch)
sha256sums=('75f5d5fda2e9607a5c77455296b41e5dd436389e20f35c37bea395d35aea0954'
            '3b2297d5a6398e468af7ed2b4fc2001a27c6598b581043f48cc45757515e7512')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/discount-mingw-building.patch
  sed -e "s/m 444/m 644/g" -i configure.inc
}

build() {
  cd "${srcdir}"/${_realname}-${pkgver}
  #MSYSTEM=MINGW 
  CC=${MINGW_PREFIX}/bin/gcc.exe \
  AC_CPP_PROG=${MINGW_PREFIX}/bin/cpp.exe \
  AC_LN_PROG="cp -pR" \
  ./configure.sh \
      --prefix=${MINGW_PREFIX} \
      --enable-all-features \
      --shared

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,include,lib}
  make PREFIX=${MINGW_PREFIX} DESTDIR="${pkgdir}" install.everything
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/
  install -Dm644 COPYRIGHT "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  find ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig -name *.pc -exec sed -i -e"s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" {} \;
}
